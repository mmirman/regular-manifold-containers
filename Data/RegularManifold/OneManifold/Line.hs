-----------------------------------------------------------------------------
-- |
-- Module      :  Data.RegularManifold.OneManifold.Line
-- Copyright   :  (c) Matthew Mirman 2012
-- License     :  GPL-3 
-- 
-- Maintainer  :  Matthew Mirman <mmirman@andrew.cmu.edu>
-- Stability   :  experimental
-- Portability :  portable
-- 
-- This module defines some functions to build and manipulate infinite lines
-- All exposed functions have the invariant that the Line passed was not
-- generated by any function not in this module.  Failure to satisfy this
-- invariant will likely result in undefined behavior.
-----------------------------------------------------------------------------

module Data.RegularManifold.OneManifold.Line 
       ( Line(..)
       , makeLine
       , rewriteNode
       , getRelative
       , rewriteRelative
       , mapLineAlternate
       ) where

import Control.UsefulCombinators
import Control.Comonad

-- | @'Line' a@ represents a viewpoint to a doubly linked 
-- data structure
-- in one dimension.  It can only be cyclic or infinite.
data Line a = Node { left  :: Line a
                   , value :: a 
                   , right :: Line a
                   }

instance Functor Line where 
  fmap f (Node l v r) = iC
    where iC = Node iL (f v) iR
          iL = mapLineL (f . value) l iC
          iR = mapLineR (f . value) r iC

instance Extend Line where
  extend f v@(Node l _ r) = iC
    where iC = Node iL (f v) iR
          iL = mapLineL f l iC
          iR = mapLineR f r iC

instance Comonad Line where
  extract = value


mapLineL f v@(Node l _ _) r = k
  where k = Node (mapLineL f l k) (f v) r

mapLineR f v@(Node _ _ r) l = k
  where k = Node l (f v) (mapLineR f r k)

-- | @'makeLine' fL fR init@ acts like 'iterate' in two dimensions
-- where @fL@ is iterated to the left, and @fR@ is iterated to the right
-- both starting with the value @init@.  The result is a doubly linked
-- line.
makeLine :: (t -> t) -> (t -> t) -> t  -> Line t
makeLine fL fR init = i
  where i = Node iL init iR
        iL = mapLineL (fL . value) i i
        iR = mapLineR (fR . value) i i

-- | @'rewriteNode' a view@ returns a new infinite list with @a@
-- as the new viewpoint and all other nodes the same as in @view@
rewriteNode :: t -> Line t -> Line t
rewriteNode a (Node l _ r) = fmap value $ makeLine left right $ Node l a r

-- | @'getRelative' n view@ returns the node @n@ nodes to the 'left'
-- or 'right' of @view@ depending on whether @n@ was negative or positive.
-- It is 'id' when @n@ is zero.
getRelative :: (Ord a , Num a) => a -> Line t -> Line t
getRelative i = composeN (if i < 0 then left else right) (abs i)

-- | @'rewriteRelative' n v view@ returns the infinite list with @v@ 
-- in the @n@ position relative to @view@.
rewriteRelative :: (Ord a , Num a) => a -> t -> Line t -> Line t
rewriteRelative i t = getRelative (-i) . rewriteNode t . getRelative i

-- | @'mapLineAlternate' fL fR line@ maps @fL@ down the left side of @line@
-- and @fR@ down the right side of @line@. It leaves the view node alone.
mapLineAlternate :: (t -> t) -> (t -> t) -> Line t -> Line t
mapLineAlternate fl fr = fmap value . makeLine left' right'
  where left' l = (left l) { value = fl $ value $ left l }
        right' r = (left r) { value = fr $ value $ right r }